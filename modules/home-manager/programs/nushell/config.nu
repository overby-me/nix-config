$env.config = {
  edit_mode: vi
  show_banner: false
  keybindings: []
}

def zellij-update-tabname [] {
    if ("ZELLIJ" in $env) {
        let pwd_path = pwd | str replace $env.HOME "~";
        let session_path = $env.ZELLIJ_SESSION_NAME | str replace --all "|" "/";
        mut $tabname = $"($pwd_path | str replace $session_path ".") ❯";

        try {
            let cmd = (commandline | into string | str substring 0..15);
            if ($cmd == "ssh") {
                let ssh = (commandline | into string | split row " " | get 1);
                $tabname = $"($ssh) ❯";
            } else if ($pwd_path | str starts-with $session_path) {
              $tabname = $"($pwd_path | str replace $session_path ".") ❯ ($cmd)";
            } else {
              $tabname = $"($pwd_path) ❯ ($cmd)";
            }
        };

        zellij action rename-tab $tabname;
    }
}

$env.config.hooks = {
    pre_execution: [
        { zellij-update-tabname }
    ],
    env_change: {
        PWD: [
            { zellij-update-tabname }
        ]
    }
    # Fix for: https://github.com/nushell/nushell/issues/11950
    display_output: {||
        if (term size).columns >= 100 {
          table -e
        } else {
          table
        }
        | if (($in | describe) =~ "^string(| .*)") and ($in | str contains (ansi cursor_position)) {
          str replace --no-expand --all (ansi cursor_position) ""
        } else {
          print -n --raw $in
        }
    }
}

$env.PATH = ($env.PATH | split row (char esep))

def --env uo [] { let res = uf | $in; cd $res }

def ghash [] {git rev-parse HEAD | tr -d '\\n' | wl-copy; git rev-parse HEAD}

def show [] {to json | jless}

def ggg [] {
  git push -f
  gh pr create --fill
  gh pr comment --body 'bors merge'
}

def dhost [num: int] {
  let known_hosts = open ~/.ssh/known_hosts | lines;
  $known_hosts | enumerate | where index != ($num - 1) | get item | save -f ~/.ssh/known_hosts
}

def gdf [branch: string] {
  gco $branch
  let elems = $branch | split row "/"
  if ($elems | length) == 4 {
    gco $"bump-($elems | last 2 | str join "-")"

  } else if ($elems | length) == 3 {
    gco $"bump-($elems | get 2)"
  }
  git commit --amend --no-edit
  git push
  gh pr create --fill
}

def bin64 [] {
  xxd -r -p | base64 -w 0
}

def unbin64 [] {
  base64 -d | xxd -p -c 0
}

def --env assume [profile?: string = ""] {
  let granted_output = assumego $profile
  let granted = $granted_output | lines | get 0 | split row " "
  load-env {
    AWS_ACCESS_KEY_ID: $granted.1,
    AWS_SECRET_ACCESS_KEY: $granted.2,
    AWS_SESSION_TOKEN: $granted.3,
    AWS_PROFILE: $granted.4,
    AWS_REGION: $granted.5,
    AWS_DEFAULT_REGION: $granted.5,
    AWS_SESSION_EXPIRATION: $granted.6,
    AWS_CREDENTIAL_EXPIRATION: $granted.6,
    GRANTED_SSO: $granted.7,
    GRANTED_SSO_START_URL: $granted.8,
    GRANTED_SSO_ROLE_NAME: $granted.9,
    GRANTED_SSO_REGION: $granted.10,
    GRANTED_SSO_ACCOUNT_ID: $granted.11,
  }
 }

def yarn-lock-update [] {
  try { grm }
  let root = git rev-parse --show-toplevel
  git reset $"($root)/.pnp.cjs" $"($root)/yarn.lock"
  yarn
  git add $"($root)/.pnp.cjs" $"($root)/yarn.lock"
}

def gco [branch_name: string] {
    git fetch origin

    let local_exists = (git show-ref --quiet $"refs/heads/($branch_name)" | complete | get exit_code) == 0
    let remote_exists = (git show-ref --quiet $"refs/remotes/origin/($branch_name)" | complete | get exit_code) == 0

    if $local_exists {
        git checkout $branch_name
    } else if $remote_exists {
        git checkout -b $branch_name --track $"origin/($branch_name)"
    } else {
        git checkout -b $branch_name
    }
}

def gcom [] {
  git fetch origin
  let default_branch = (git symbolic-ref refs/remotes/origin/HEAD | str replace "refs/remotes/origin/" "")
  gco $default_branch
}

def grm [] {
  git fetch origin
  let default_branch = (git symbolic-ref refs/remotes/origin/HEAD | str replace "refs/remotes/origin/" "")
  git rebase $default_branch
}

def greset [] {
  git fetch origin
  let default_branch = (git symbolic-ref refs/remotes/origin/HEAD | str replace "refs/remotes/origin/" "")
  let base = (git merge-base HEAD $default_branch)
  git reset --soft $base
}

# Use Zellij-cwd in Zed terminal
if ($env.ZED_TERM?  == "true") and ($env.ZELLIJ? == null) {
    zellij-cwd
}
